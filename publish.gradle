def stdout = new ByteArrayOutputStream()
exec {
    commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
    standardOutput = stdout
}
def branch=stdout.toString().trim();

apply plugin: "signing"
apply plugin: "maven-publish"

ext {
    snapshotUrl = "https://oss.sonatype.org/content/repositories/snapshots"
    artifactorySnapshotUrl = "http://artifactory.krypt.int/artifactory/libs-snapshot-local"
    artifactoryReleaseUrl = "http://artifactory.krypt.int/artifactory/libs-release-local"
    releaseUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
    localUrl = "file:/${project.projectDir}/artifacts"
}

if( ( (branch.equals("develop") || branch.equals("master") || branch.contains("release/") ) ) &&
    ( System.getenv("bamboo_JAR_SIGNING_KEYRING") != null) &&
    ( System.getenv("bamboo_JAR_SIGNING_PASSWORD") !=null ) &&
    ( System.getenv("bamboo_SONATYPE_USERNAME") !=null ) &&
    ( System.getenv("bamboo_SONATYPE_PASSWORD") !=null ) &&
    ( System.getenv("bamboo_ARTIFACTORY_USERNAME") !=null ) &&
    ( System.getenv("bamboo_ARTIFACTORY_PASSWORD") != null)) {

    ext."signing.keyId" = 'FDEB5A45'
    ext."signing.secretKeyRingFile" = "$System.env.bamboo_JAR_SIGNING_KEYRING"
    ext."signing.password" = "$System.env.bamboo_JAR_SIGNING_PASSWORD"

    configurations {
        pom
    }

    signing {
        sign configurations.archives
    }

    signing {
        sign configurations.pom
    }

    publishing {
        repositories {
            maven {
                url branch.equals("master") ? releaseUrl : snapshotUrl
                credentials {
                    username = "$System.env.bamboo_SONATYPE_USERNAME"
                    password = "$System.env.bamboo_SONATYPE_PASSWORD"
                }
            }
            //maven {
                //url branch.equals("master") ? artifactoryReleaseUrl : artifactorySnapshotUrl
                //credentials {
                    //username = "$System.env.bamboo_ARTIFACTORY_USERNAME"
                    //password = "$System.env.bamboo_ARTIFACTORY_PASSWORD"
                //}
            //}
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact ('README.md') {
                    classifier = 'README'
                    extension  = 'md'
                }

                artifact javadocJar
                artifact sourcesJar
                artifact testJar

                pom.withXml {
                    asNode().children().last() + {
                        resolveStrategy = Closure.DELEGATE_FIRST
                        name 'rhizome'
                        description 'Java Framework for building clustered RESTful web applications'
                        url 'https://github.com/kryptnostic/rhizome'
                        scm {
                            url 'https://github.com/kryptnostic/rhizome'
                            connection 'https://github.com/kryptnostic/rhizome'
                            developerConnection 'https://github.com/kryptnostic/rhizome'
                        }
                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/license/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id 'geekbeast'
                                name 'Matthew Tamayo-Rios'
                                email 'matthew@kryptnostic.com'
                            }
                            developer {
                                id 'nickdhewitt'
                                name 'Nick Hewitt'
                                email 'nick@kryptnostic.com'
                            }
                            developer {
                                id 'rbuckheit'
                                name 'Ryan Buckheit'
                                email 'ryan@kryptnostic.com'
                            }
                            developer {
                                id 'makosblade'
                                name 'Drew Bailey'
                                email 'drew@kryptnostic.com'
                            }
                        }
                    }
                }
            }
        }
    }
}
